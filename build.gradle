plugins {
    id "java"
    id "idea"
    id "de.undercouch.download" version "4.1.2"
    id 'edu.sc.seis.launch4j' version '2.5.0'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.0'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

repositories {
    maven {
        url "https://repo.gradle.org/gradle/libs-releases-local/"
    }
    flatDir { dirs 'lib' }
}

project.ext.mcreatorconf = new Properties()
file('src/main/resources/mcreator.conf').withInputStream { project.mcreatorconf.load(it) }

group = 'net.mcreator'
version = project.mcreatorconf.getProperty('mcreator')
project.ext.builddate = new Date().format('wwuHH')

javadoc.source = sourceSets.main.allJava
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

// workaround for IDEA-265203
System.setProperty("user.dir", projectDir.toString())

idea {
    module {
        inheritOutputDirs = true
        excludeDirs = [file(".gradle"), file(".idea"), file(".github"), file("build"),
                       file("gradle"), file("jdk"), file("license"), file("logs")] as Set<File>
    }
}

configurations {
    testImplementation.extendsFrom compileOnly

    provided
    compileOnly.extendsFrom provided
    runtimeOnly.extendsFrom provided

    export.extendsFrom implementation
    export.canBeResolved = true

    win64
    mac64
    linux64
}

dependencies {
    // from lib folder
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    // from maven
    implementation group: 'commons-io', name: 'commons-io', version: '2.6' // 2.7+ problem with : in filenames: NTFS ADS separator
    implementation group: 'foxtrot', name: 'foxtrot-core', version: '4.0'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.31'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'org.jboss.forge.roaster', name: 'roaster-api', version: '2.22.2.Final'
    implementation group: 'org.jboss.forge.roaster', name: 'roaster-jdt', version: '2.22.2.Final'
    implementation group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.15'
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.1.2'
    implementation group: 'com.fifesoft', name: 'autocomplete', version: '3.1.1'
    implementation(group: 'com.fifesoft', name: 'languagesupport', version: '3.1.1') { dep -> dep.exclude group: 'org.mozilla', module: 'rhino' }
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.9' // https://github.com/ronmamo/reflections/issues/81
    implementation group: 'de.javagl', name: 'obj', version: '0.3.0'
    implementation group: 'com.univocity', name: 'univocity-parsers', version: '2.9.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.11.0.202103091610-r'
    implementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.30'
    implementation group: 'org.gradle', name: 'gradle-tooling-api', version: '7.1'
    implementation group: 'net.java.balloontip', name: 'balloontip', version: '1.2.4.1'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.16.1'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark-ext-autolink', version: '0.16.1'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark-ext-gfm-tables', version: '0.16.1'
    implementation group: 'club.minnced', name: 'java-discord-rpc', version: '2.0.2'

    // test dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.1'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'

    // JFX natives
    win64 group: 'org.openjfx', name: 'javafx-base', version: '15', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-controls', version: '15', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-graphics', version: '15', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-media', version: '15', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-web', version: '15', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-swing', version: '15', classifier: 'win'

    mac64 group: 'org.openjfx', name: 'javafx-base', version: '15', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-controls', version: '15', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-graphics', version: '15', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-media', version: '15', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-web', version: '15', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-swing', version: '15', classifier: 'mac'

    linux64 group: 'org.openjfx', name: 'javafx-base', version: '15', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-controls', version: '15', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-graphics', version: '15', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-media', version: '15', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-web', version: '15', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-swing', version: '15', classifier: 'linux'
}

javafx {
    version = "15"
    modules = [ 'javafx.web', 'javafx.swing' ]
    configuration = 'provided' // we provide natives during deployment or with SDK
}

test {
    useJUnitPlatform()

    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    maxHeapSize = "1024m"
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath
}

task runMCreator(type: JavaExec, dependsOn: jar) {
    classpath += sourceSets.main.runtimeClasspath
    main = "net.mcreator.Launcher"

}

apply from: 'platform/setup.gradle'
apply from: 'platform/export.gradle'
