plugins {
    id "java"
    id "idea"
    id "de.undercouch.download" version "5.3.1"
    id 'edu.sc.seis.launch4j' version '2.5.4'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

repositories {
    maven { url "https://repo.gradle.org/gradle/libs-releases" } // for gradle-tooling-api
    flatDir { dirs 'lib' }
}

project.ext.mcreatorconf = new Properties()
file('src/main/resources/mcreator.conf').withInputStream { project.mcreatorconf.load(it) }

group = 'net.mcreator'
version = project.mcreatorconf.getProperty('mcreator')
project.ext.builddate = new Date().format('wwuHH')

javadoc.source = sourceSets.main.allJava
compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }

sourceCompatibility = 17
targetCompatibility = 17

// workaround for IDEA-265203
System.setProperty("user.dir", projectDir.toString())

idea {
    module {
        inheritOutputDirs = true

        // define exclude dirs
        excludeDirs += file(".gradle")
        excludeDirs += file(".idea")
        excludeDirs += file("build")
        excludeDirs += file("gradle")
        excludeDirs += file("jdk")
        excludeDirs += file("license")
        excludeDirs += file("logs")

        // additional source dirs
        sourceDirs += file("plugins/mcreator-core/blockly/js")
    }
}

configurations {
    testImplementation.extendsFrom compileOnly

    provided
    compileOnly.extendsFrom provided
    runtimeOnly.extendsFrom provided

    export.extendsFrom implementation
    export.canBeResolved = true

    win64
    mac64
    linux64
}

dependencies {
    // from lib folder
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    // from maven
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.32'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'org.jboss.forge.roaster', name: 'roaster-api', version: '2.28.0.Final'
    implementation group: 'org.jboss.forge.roaster', name: 'roaster-jdt', version: '2.28.0.Final'
    implementation group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.15'
    implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'de.javagl', name: 'obj', version: '0.3.0'
    implementation group: 'com.univocity', name: 'univocity-parsers', version: '2.9.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.19.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '6.4.0.202211300538-r'
    implementation group: 'org.slf4j', name: 'slf4j-nop', version: '2.0.6'
    implementation group: 'org.gradle', name: 'gradle-tooling-api', version: '8.0.2'
    implementation group: 'net.java.balloontip', name: 'balloontip', version: '1.2.4.1'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.17.0'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark-ext-autolink', version: '0.17.0'
    implementation group: 'com.atlassian.commonmark', name: 'commonmark-ext-gfm-tables', version: '0.17.0'
    implementation group: 'com.fifesoft', name: 'rsyntaxtextarea', version: '3.3.2'
    implementation group: 'com.fifesoft', name: 'autocomplete', version: '3.3.1'
    implementation(group: 'com.fifesoft', name: 'languagesupport', version: '3.3.0') {
        dep -> dep.exclude group: 'org.mozilla', module: 'rhino'
    }
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.13.0' // needed by discord-rpc.jar

    // test dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.2'

    // JFX natives
    win64 group: 'org.openjfx', name: 'javafx-base', version: '19', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-controls', version: '19', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-graphics', version: '19', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-media', version: '19', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-web', version: '19', classifier: 'win'
    win64 group: 'org.openjfx', name: 'javafx-swing', version: '19', classifier: 'win'

    mac64 group: 'org.openjfx', name: 'javafx-base', version: '19', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-controls', version: '19', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-graphics', version: '19', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-media', version: '19', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-web', version: '19', classifier: 'mac'
    mac64 group: 'org.openjfx', name: 'javafx-swing', version: '19', classifier: 'mac'

    linux64 group: 'org.openjfx', name: 'javafx-base', version: '19', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-controls', version: '19', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-graphics', version: '19', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-media', version: '19', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-web', version: '19', classifier: 'linux'
    linux64 group: 'org.openjfx', name: 'javafx-swing', version: '19', classifier: 'linux'
}

javafx {
    version = "19"
    modules = ['javafx.web', 'javafx.swing']
    configuration = 'provided' // we provide natives during deployment or with SDK
}

test {
    useJUnitPlatform()

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    // default max heap is VM RAM / 4, which may be too low on low RAM VMs
    maxHeapSize = "2048m"

    testLogging {
        // https://stackoverflow.com/a/48709645/1627085
        events "passed", "skipped", "failed"
        showStandardStreams = true

        showCauses true
        showStackTraces true
        showExceptions true
        exceptionFormat "full"

        debug {
            events "started", "failed", "passed", "skipped", "standard_error", "standard_out"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtimeClasspath

    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.register('runMCreator', JavaExec.class) {
    dependsOn jar

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    classpath += sourceSets.main.runtimeClasspath

    mainClass.set("net.mcreator.Launcher")
}

apply from: 'platform/setup.gradle'
apply from: 'platform/export.gradle'
