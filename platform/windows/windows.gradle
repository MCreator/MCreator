import org.apache.tools.ant.taskdefs.condition.Os

task dummyTask {
}

createExe {
    outfile = 'mcreator.exe'
    mainClassName = 'net.mcreator.Launcher'
    dontWrapJar = true
    copyConfigurable = dummyTask.outputs.files
    classpath = [
            "./lib/*"
    ]
    libraryDir = "null"
    stayAlive = true
    jdkPreference = "jdkOnly"
    bundledJrePath = 'jdk'

    bundledJre64Bit = true
    jreRuntimeBits = 64

    errTitle = 'MCreator'
    supportUrl = 'https://mcreator.net/support'

    fileDescription = "MCreator " + (String) project.mcreatorconf.getProperty('mcreator')
    productName = "MCreator"
    companyName = "Pylo"

    icon = "${projectDir}/src/main/resources/net/mcreator/ui/res/icon.ico"
}

task exportWin64(type: Copy, dependsOn: [downloadJDKWin64, createExe, jar, exportPlugins]) {
    into 'build/export/win64'
    into('') {
        from file('LICENSE.txt')
        from file('platform/windows/mcreator.bat')
        from file('build/launch4j/mcreator.exe')
    }
    into('lib') {
        with copySpec {
            from 'build/libs'
            from configurations.export
            from configurations.win64
        }
    }
    into('plugins') { from 'build/plugins/' }
    into('license') { with copySpec { from 'license' } }
    into('jdk') { with copySpec { from 'jdk/jdk11_win_64' } exclude "jre/lib/tools.jar" }
}

task zipWin64(type: Zip, dependsOn: exportWin64) {
    from 'build/export/win64/'
    into 'MCreator' + (String) project.mcreatorconf.getProperty('mcreator').replace(".", "").replace(" ", "")
    destinationDirectory = file('build/export/')
    archiveFileName = 'MCreator ' + (String) project.mcreatorconf.getProperty('mcreator') + ' Windows 64bit.zip'
}

task buildInstallerWin64(type: Exec, dependsOn: [downloadNSIS, exportWin64]) {
    doFirst {
        copy {
            from file('platform/windows/installer/install.nsi') into 'build/export' rename('install.nsi', 'install64.nsi') filter { line ->
                line.replace('%year%', new Date().format('yyyy'))
                        .replace('%build%', project.builddate)
                        .replace('%mcreator%', (String) project.mcreatorconf.getProperty('mcreator'))
            }
        }
    }
    
    def nsis_binary = '';
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        nsis_binary = projectDir.toString() + '/build/tools/nsis/makensis';
    } else {
        nsis_binary = 'makensis';
    }
    commandLine nsis_binary, 'build/export/install64.nsi'
    
    doLast { delete 'build/export/install64.nsi' }
}

task exportWindowsAll(type: DefaultTask, group: 'export') {
    dependsOn zipWin64
    dependsOn buildInstallerWin64

    doLast {
        delete 'build/export/win64'
    }
}

task exportWindowsZIP(type: DefaultTask, group: 'export') {
    dependsOn zipWin64
    doLast {
        delete 'build/export/win64'
    }
}
