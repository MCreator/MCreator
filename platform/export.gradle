tasks.register('cleanupPlugins') {
    doFirst {
        delete 'build/plugins/'
    }
}

// export  all plugins
file('plugins').traverse([maxDepth: 0, type: groovy.io.FileType.DIRECTORIES]) {
    tasks.create('export_plugin_' + it.getName().replace('.', ''), Zip.class) { task ->
        task.archiveFileName = it.getName() + '.zip'
        task.destinationDirectory = file('build/plugins/')
        task.from 'plugins/' + it.getName()
        task.group "mcreatorplugins"
        task.mustRunAfter cleanupPlugins
    }
}

tasks.register('exportPlugins') {
    dependsOn tasks.matching { Task t -> t.name.startsWith("export_plugin_") }
    dependsOn cleanupPlugins
}

tasks.register('exportReleasePreTask') {
    dependsOn testClasses
    doFirst {
        delete 'build/export/'
        mkdir 'build/export/'
    }
}

jar {
    archiveFileName = 'mcreator.jar'
    manifest {
        attributes(
                'Main-Class': 'net.mcreator.Launcher',
                'MCreator-Version': project.mcreatorconf.getProperty('mcreator'),
                'Build-Date': project.builddate
        )
    }
    dependsOn exportReleasePreTask
}

tasks.register('exportRelease') {
    group 'export'
    doLast {
        project.logger.lifecycle('\nFINISHED export for MCreator ' + (String) project.mcreatorconf.getProperty('mcreator'))
    }
}

tasks.register('exportSnapshot') {
    group 'export'

    if (gradle.startParameter.taskNames.contains(name)) { // only run this if the task started was this one
        project.mcreatorconf.setProperty('mcreator', "EAP " + (String) project.mcreatorconf.getProperty('mcreator') + new Date().format(" 'b'wwuHH"))
        file('src/main/resources/snapshot.conf') << (String) project.mcreatorconf.getProperty('mcreator')
    }

    doLast {
        delete 'src/main/resources/snapshot.conf'
        project.logger.lifecycle('\nFINISHED export for MCreator ' + (String) project.mcreatorconf.getProperty('mcreator'))
    }
}

tasks.register('generateExportReport') {
    doLast {
        def report = ""

        def fg = new LinkedHashSet<String>()
        def bd = new LinkedHashSet<String>()
        def dp = new LinkedHashSet<String>()
        file('plugins').traverse([maxDepth: 1, type: groovy.io.FileType.DIRECTORIES]) {
            def fn = it.getName()
            if (fn.startsWith("addon-")) {
                bd.add(fn.substring(fn.lastIndexOf("-") + 1))
            } else if (fn.startsWith("datapack-")) {
                dp.add(fn.substring(fn.lastIndexOf("-") + 1))
            } else if (fn.startsWith("forge-")) {
                fg.add(fn.substring(fn.lastIndexOf("-") + 1))
            }
        }

        report += 'mcreator=' + version.toString() + '.' + project.builddate.toString() + '\n'
        report += 'version=' + (String) project.mcreatorconf.getProperty('mcreator').toString() + '\n'
        report += 'builddate=' + project.builddate.toString() + '\n'
        report += 'minecraft=Minecraft Forge ' + String.join("/", fg) + ', Data Packs ' + String.join("/", dp) + ', and Bedrock Edition ' + String.join("/", bd) + '\n'

        new File("build/reports").mkdirs()
        new File("build/reports/export.txt").text = report
    }
}

apply from: 'platform/linux/linux.gradle'
apply from: 'platform/mac/mac.gradle'

exportRelease.dependsOn(exportMac)
exportRelease.dependsOn(exportLinux)
exportRelease.dependsOn(generateExportReport)

exportSnapshot.dependsOn(exportMac)
exportSnapshot.dependsOn(exportLinux)
exportSnapshot.dependsOn(generateExportReport)
