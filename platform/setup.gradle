import org.apache.tools.ant.taskdefs.condition.Os

// Can not update to 21.0.10 due to https://youtrack.jetbrains.com/projects/JBR/issues/JBR-8397, https://youtrack.jetbrains.com/projects/JBR/issues/JBR-8855
// Tests fail on Windows occasionally due to this bug
def jbr21_win_64 = 'jbrsdk_jcef-21.0.9-windows-x64-b1163.94'
def jbr21_linux_64 = 'jbrsdk_jcef-21.0.9-linux-x64-b1163.94'
def jbr21_mac_x64 = 'jbrsdk_jcef-21.0.9-osx-x64-b1163.94'
def jbr21_mac_aarch64 = 'jbrsdk_jcef-21.0.9-osx-aarch64-b1163.94'

// Binaries needed on Windows for export (install them using Gradle since Windows does not have a package manager)
def nsis_url = 'https://netcologne.dl.sourceforge.net/project/nsis/NSIS%203/3.09/nsis-3.09.zip'
def nsis_lockedlist_plugin_url = 'https://nsis.sourceforge.io/mediawiki/images/d/d3/LockedList.zip'
def mkisofs_url = 'https://master.dl.sourceforge.net/project/mkisofs-md5/mkisofs-md5-v2.01/mkisofs-md5-2.01-Binary.zip'

tasks.register('downloadJDKWin64') {
    group = 'setup'
    doLast {
        if (!file('jdk/jbr21_win_64/bin/java.exe').exists()) {
            mkdir 'jdk/jbr21_win_64'

            download.run {
                src "https://cache-redirector.jetbrains.com/intellij-jbr/${jbr21_win_64}.zip"
                dest 'build/tmp/jbr21_win_64'
                overwrite true
            }
            copy { from zipTree('build/tmp/jbr21_win_64') into file('jdk/jbr21_win_64/') }
            delete 'build/tmp/jbr21_win_64'

            copy { from file("jdk/jbr21_win_64/${jbr21_win_64}") into file('jdk/jbr21_win_64/') }
            delete "jdk/jbr21_win_64/${jbr21_win_64}"

            // Space saving
            delete fileTree('jdk/jbr21_win_64/jmods') { exclude 'java.base.jmod' }
        }
    }
}

tasks.register('downloadJDKLinux64') {
    group = 'setup'
    doLast {
        if (!file('jdk/jbr21_linux_64/bin/java').exists()) {
            mkdir 'jdk/jbr21_linux_64'

            download.run {
                src "https://cache-redirector.jetbrains.com/intellij-jbr/${jbr21_linux_64}.tar.gz"
                dest 'build/tmp/jbr21_linux_64'
                overwrite true
            }
            copy { from tarTree(resources.gzip('build/tmp/jbr21_linux_64')) into file('jdk/jbr21_linux_64/') }
            delete 'build/tmp/jbr21_linux_64'

            copy { from file("jdk/jbr21_linux_64/${jbr21_linux_64}") into file('jdk/jbr21_linux_64/') }
            delete "jdk/jbr21_linux_64/${jbr21_linux_64}"

            // Space saving
            delete fileTree('jdk/jbr21_linux_64/jmods') { exclude 'java.base.jmod' }
        }
    }
}

tasks.register('downloadJDKMacX64') {
    group = 'setup'
    doLast {
        if (!file('jdk/jbr21_mac_x64/').exists()) {
            mkdir 'jdk/jbr21_mac_x64'

            download.run {
                src "https://cache-redirector.jetbrains.com/intellij-jbr/${jbr21_mac_x64}.tar.gz"
                dest 'build/tmp/jbr21_mac_x64'
                overwrite true
            }
            copy { from tarTree(resources.gzip('build/tmp/jbr21_mac_x64')) into file('jdk/jbr21_mac_x64/') }
            delete 'build/tmp/jbr21_mac_x64'

            copy { from file("jdk/jbr21_mac_x64/${jbr21_mac_x64}") into file('jdk/jbr21_mac_x64/') }
            delete "jdk/jbr21_mac_x64/${jbr21_mac_x64}/"

            // Space saving
            delete fileTree('jdk/jbr21_mac_x64/Contents/Home/jmods') { exclude 'java.base.jmod' }
        }
    }
}

tasks.register('downloadJDKMacAarch64') {
    group = 'setup'
    doLast {
        if (!file('jdk/jbr21_mac_aarch64/').exists()) {
            mkdir 'jdk/jbr21_mac_aarch64'

            download.run {
                src "https://cache-redirector.jetbrains.com/intellij-jbr/${jbr21_mac_aarch64}.tar.gz"
                dest 'build/tmp/jbr21_mac_aarch64'
                overwrite true
            }
            copy { from tarTree(resources.gzip('build/tmp/jbr21_mac_aarch64/')) into file('jdk/jbr21_mac_aarch64/') }
            delete 'build/tmp/jbr21_mac_aarch64'

            copy { from file("jdk/jbr21_mac_aarch64/${jbr21_mac_aarch64}") into file('jdk/jbr21_mac_aarch64/') }
            delete "jdk/jbr21_mac_aarch64/${jbr21_mac_aarch64}/"

            // Space saving
            delete fileTree('jdk/jbr21_mac_aarch64/Contents/Home/jmods') { exclude 'java.base.jmod' }
        }
    }
}

// export environment setup task
tasks.register('downloadNSIS') {
    group = 'setup'
    doLast {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            if (!file('build/tools/nsis/').exists()) {
                download.run {
                    src nsis_url
                    dest 'build/tmp/nsis'
                    overwrite true
                }
                copy { from zipTree(file('build/tmp/nsis')) into file('build/tools/') }
                file('build/tools/nsis-3.09').renameTo(file('build/tools/nsis'))
                delete 'build/tmp/nsis'

                download.run {
                    src nsis_lockedlist_plugin_url
                    dest 'build/tmp/nsis_lockedlist_plugin'
                    overwrite true
                }
                copy { from zipTree(file('build/tmp/nsis_lockedlist_plugin')) into file('build/tools/nsis/') }
                delete 'build/tmp/nsis_lockedlist_plugin'
            }
        }
    }
}

tasks.register('downloadMKISOFS') {
    group = 'setup'
    doLast {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            if (!file('build/tools/mkisofs/').exists()) {
                download.run {
                    src mkisofs_url
                    dest 'build/tmp/mkisofs'
                    overwrite true
                }
                copy {
                    from zipTree(file('build/tmp/mkisofs'))
                    into file('build/tools/')
                }
                file('build/tools/Binary').renameTo(file('build/tools/mkisofs'))
                delete 'build/tmp/mkisofs'
            }
        }
    }
}

tasks.register('initialSetup') {
    group = 'setup'

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        dependsOn downloadJDKWin64
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        dependsOn downloadJDKMacAarch64, downloadJDKMacX64
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        dependsOn downloadJDKLinux64
    }
}

if (idea.project != null) {
    idea.project.settings.taskTriggers {
        afterSync initialSetup
    }
}