import org.apache.tools.ant.taskdefs.condition.Os

def exportPath = 'build/export/mac64/MCreator.app/Contents/'

static void convertCRLF(File input, File out) {
    out << input.text.replaceAll('\r\n', '\n')
}

tasks.register('exportMac64', Copy.class) {
    dependsOn downloadJDKMac64
    dependsOn jar
    dependsOn exportPlugins

    into exportPath
    into('') {
        from file('platform/mac/Info.plist') rename('Info.plist', 'Info.plistdos') filter { line ->
            line.replace('%mcreator%', (String) project.mcreatorconf.getProperty('mcreator'))
        }
        from file('LICENSE.txt')
    }
    into('Resources') {
        from file('platform/mac/mcreatorapp.icns')
        from file('platform/mac/mcreator.icns')
    }
    into('MacOS') {
        from file('platform/mac/mcreator_arm64') rename('mcreator_arm64', 'mcreator')
        from file('LICENSE.txt')
    }
    into('MacOS/plugins') { from 'build/plugins/' }
    into('MacOS/license') { from 'license' }
    into('MacOS/lib') {
        from 'build/libs'
        from configurations.export
        from configurations.mac64
    }
    into('MacOS/jdk/') { from 'jdk/jdk17_mac_64/' }
    doLast {
        convertCRLF(file(exportPath + '/Info.plistdos'), file(exportPath + '/Info.plist'))
        delete exportPath + '/Info.plistdos'
    }
}

def exportPathX64 = 'build/export/mac_x64/MCreator.app/Contents/'

tasks.register('exportMacX64', Copy.class) {
    dependsOn downloadJDKMacX64
    dependsOn jar
    dependsOn exportPlugins

    into exportPathX64
    into('') {
        from file('platform/mac/Info.plist') rename('Info.plist', 'Info.plistdos') filter { line ->
            line.replace('%mcreator%', (String) project.mcreatorconf.getProperty('mcreator'))
        }
        from file('LICENSE.txt')
    }
    into('Resources') {
        from file('platform/mac/mcreatorapp.icns')
        from file('platform/mac/mcreator.icns')
    }
    into('MacOS') {
        from file('platform/mac/mcreator_x86') rename('mcreator_x86', 'mcreator')
        from file('LICENSE.txt')
    }
    into('MacOS/plugins') { from 'build/plugins/' }
    into('MacOS/license') { from 'license' }
    into('MacOS/lib') {
        from 'build/libs'
        from configurations.export
        from configurations.mac64
    }
    into('MacOS/jdk') { from 'jdk/jdk17_mac_x64/' }
    doLast {
        convertCRLF(file(exportPathX64 + '/Info.plistdos'), file(exportPathX64 + '/Info.plist'))
        delete exportPathX64 + '/Info.plistdos'
    }
}

tasks.register('dmgMac64') {
    dependsOn downloadMKISOFS
    dependsOn exportMac64, exportMacX64

    doLast {
        def mkisofs_binary
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            mkisofs_binary = projectDir.toString() + '/build/tools/mkisofs/Sample/mkisofs'
        } else { // we are on Nix system
            mkisofs_binary = 'mkisofs'

            // chmod needed on executable as mkisofs does not +x on Nix systems
            exec { commandLine('chmod', '+x', exportPath + '/MacOS/mcreator') }
            exec { commandLine('chmod', '+x', exportPathX64 + '/MacOS/mcreator') }
        }

        // Create arm64 DMG
        ant.exec(executable: mkisofs_binary, failonerror: true) {
            arg(value: '-r')
            arg(value: '-D')
            arg(value: '-o')
            arg(value: 'build/export/MCreator ' + (String) project.mcreatorconf.getProperty('mcreator') + ' Mac arm64.dmg')
            arg(value: '-mac-name')
            arg(value: '-V')
            arg(value: 'MCreator ' + (String) project.mcreatorconf.getProperty('mcreator'))
            arg(value: '-apple')
            arg(value: '-v')
            arg(value: new File(buildDir, 'export/mac64'))
        }

        // Create x86_64 DMG
        ant.exec(executable: mkisofs_binary, failonerror: true) {
            arg(value: '-r')
            arg(value: '-D')
            arg(value: '-o')
            arg(value: 'build/export/MCreator ' + (String) project.mcreatorconf.getProperty('mcreator') + ' Mac x86_64.dmg')
            arg(value: '-mac-name')
            arg(value: '-V')
            arg(value: 'MCreator ' + (String) project.mcreatorconf.getProperty('mcreator'))
            arg(value: '-apple')
            arg(value: '-v')
            arg(value: new File(buildDir, 'export/mac_x64'))
        }
    }
}

tasks.register('exportMac') {
    group 'export'

    dependsOn dmgMac64

    doLast {
        delete 'build/export/mac64'
        delete 'build/export/mac_x64'
    }
}