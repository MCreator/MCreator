import org.apache.tools.ant.taskdefs.condition.Os

static void convertCRLF(File input, File out) {
    out << input.text.replaceAll('\r\n', '\n')
}

tasks.register('exportMacX64', Copy.class) {
    dependsOn downloadJDKMacX64
    dependsOn jar
    dependsOn exportPlugins

    def exportPathX64 = 'build/export/mac_x64/MCreator.app/Contents/'

    into exportPathX64
    into('') {
        from file('platform/mac/Info.plist') rename('Info.plist', 'Info.plistdos') filter { line ->
            line.replace('%mcreator%', (String) project.mcreatorconf.getProperty('mcreator'))
        }
        from file('LICENSE.txt')
    }
    into('Resources') {
        from file('platform/mac/mcreatorapp.icns')
        from file('platform/mac/mcreator.icns')
    }
    into('MacOS') {
        from file('platform/mac/mcreator_x64') rename('mcreator_x64', 'mcreator')
        from file('LICENSE.txt')
    }
    into('MacOS/plugins') { from 'build/plugins/' }
    into('MacOS/license') { from 'license' }
    into('MacOS/lib') {
        from 'build/libs'
        from configurations.export
        from configurations.macX64
    }
    into('MacOS/jdk') { from 'jdk/jdk17_mac_x64/' }
    doLast {
        // fix line endings where needed
        convertCRLF(file(exportPathX64 + '/Info.plistdos'), file(exportPathX64 + '/Info.plist'))
        delete exportPathX64 + '/Info.plistdos'

        // chmod needed on executable as mkisofs does not +x on Nix systems
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            exec { commandLine('chmod', '+x', exportPathX64 + '/MacOS/mcreator') }
            exec { commandLine('chmod', '+x', 'platform/mac/disk/create-dmg.sh') }
        }

        // Create x64 DMG
        exec {
            executable 'platform/mac/disk/create-dmg.sh'
            args "build/export/MCreator" + (String) project.mcreatorconf.getProperty('mcreator') + " Mac x86_64.dmg", 'build/export/mac_x64'
        }
    }
}

tasks.register('exportMacAarch64', Copy.class) {
    dependsOn downloadJDKMacAarch64
    dependsOn jar
    dependsOn exportPlugins

    def exportPathAarch64 = 'build/export/mac_aarch64/MCreator.app/Contents/'

    into exportPathAarch64
    into('') {
        from file('platform/mac/Info.plist') rename('Info.plist', 'Info.plistdos') filter { line ->
            line.replace('%mcreator%', (String) project.mcreatorconf.getProperty('mcreator'))
        }
        from file('LICENSE.txt')
    }
    into('Resources') {
        from file('platform/mac/mcreatorapp.icns')
        from file('platform/mac/mcreator.icns')
    }
    into('MacOS') {
        from file('platform/mac/mcreator_aarch64') rename('mcreator_aarch64', 'mcreator')
        from file('LICENSE.txt')
    }
    into('MacOS/plugins') { from 'build/plugins/' }
    into('MacOS/license') { from 'license' }
    into('MacOS/lib') {
        from 'build/libs'
        from configurations.export
        from configurations.macAarch64
    }
    into('MacOS/jdk/') { from 'jdk/jdk17_mac_aarch64/' }
    doLast {
        // fix line endings where needed
        convertCRLF(file(exportPathAarch64 + '/Info.plistdos'), file(exportPathAarch64 + '/Info.plist'))
        delete exportPathAarch64 + '/Info.plistdos'

        // chmod needed on executable as mkisofs does not +x on Nix systems
        if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
            exec { commandLine('chmod', '+x', exportPathAarch64 + '/MacOS/mcreator') }
        }

        // Create Aarch64 DMG
        exec {
            executable 'platform/mac/disk/create-dmg.sh'
            args "build/export/MCreator" + (String) project.mcreatorconf.getProperty('mcreator') + " Mac aarch64.dmg", 'build/export/mac_aarch64'
        }
    }
}

tasks.register('exportMac') {
    group 'export'

    dependsOn exportMacX64
    dependsOn exportMacAarch64

    doLast {
        delete 'build/export/mac_x64'
        delete 'build/export/mac_aarch64'
    }
}